buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "gradle.plugin.seek:gradle-aws-plugin:0.0.10"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

apply plugin: 'seek.cloudformation'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/versioning.gradle'

group = 'seek'

import static seek.aws.config.Lookup.lookup
import seek.aws.s3.UploadFile

ext {
    service = 'example-project'
    lambdaArtefactKey = "${service}/${version}/${service}.jar"
}

aws {
    region 'ap-southeast-2'
}

config {
    files fileTree('config').include('*.conf')
}

cloudFormation {
    stackName service
    templateFile file('src/main/cloudformation/application.yaml')
    policyFile file('src/main/cloudformation/policy.json')
    tags (['Owner', 'Project', 'Version'])
}

task uploadLambdaJar(type: UploadFile, dependsOn: shadowJar) {
    bucket lookup('buildBucket')
    key lambdaArtefactKey
    file shadowJar.archivePath
}

task deploy(dependsOn: createOrUpdateStack) {
    group 'Deployment'
    description 'Top level deployment task'
}

shadowJar.dependsOn check
createOrUpdateStack.dependsOn uploadLambdaJar
