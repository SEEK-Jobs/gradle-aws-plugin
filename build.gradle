plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
    id 'com.github.maiflai.scalatest' version '0.19'
}

apply plugin: 'scala'
apply plugin: 'maven'

group = 'seek'
ext.baseVersion = file('version.txt').text.trim()
version = "${baseVersion}-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
}

ext {
    awsVersion = '1.12.497'
    catsVersion = '1.1.0'
    catsEffectVersion = '1.2.0'
    fs2Version = '1.0.3'
    circeVersion = '0.11.1'
    kindProjectorVersion = "0.9.9"
}

configurations {
    scalaCompiler
}

configurations.scalaCompiler.transitive = false

configurations.all {
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'log4j', module: 'log4j'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
    compile gradleApi()
    compile "org.scala-lang:scala-library:${scalaVersion}"
    compile "com.typesafe.scala-logging:scala-logging_${scalaBuildVersion}:3.5.0"
    compile "org.typelevel:cats-core_${scalaBuildVersion}:${catsVersion}"
    compile "org.typelevel:cats-effect_${scalaBuildVersion}:${catsEffectVersion}"
    compile "co.fs2:fs2-core_${scalaBuildVersion}:${fs2Version}"
    compile "co.fs2:fs2-io_${scalaBuildVersion}:${fs2Version}"
    compile "io.circe:circe-core_${scalaBuildVersion}:${circeVersion}"
    compile "io.circe:circe-generic_${scalaBuildVersion}:${circeVersion}"
    compile "io.circe:circe-generic-extras_${scalaBuildVersion}:${circeVersion}"
    compile "io.circe:circe-parser_${scalaBuildVersion}:${circeVersion}"
    compile "io.circe:circe-yaml_${scalaBuildVersion}:0.7.0"
    compile "com.typesafe:config:1.3.3"
    compile "com.github.pureconfig:pureconfig_${scalaBuildVersion}:0.9.0"
    compile "com.amazonaws:aws-java-sdk-s3:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-autoscaling:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-cloudformation:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-ssm:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-sts:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-sns:${awsVersion}"
    compile "org.spire-math:kind-projector_${scalaBuildVersion}:${kindProjectorVersion}"

    compileOnly "com.github.dwickern:scala-nameof_${scalaBuildVersion}:1.0.3"

    testCompile "org.scalatest:scalatest_${scalaBuildVersion}:3.0.4"
    testCompile "org.scalacheck:scalacheck_${scalaBuildVersion}:1.13.5"
    testRuntime 'org.pegdown:pegdown:1.6.0'

    scalaCompiler "org.spire-math:kind-projector_${scalaBuildVersion}:${kindProjectorVersion}"
}

def scalaCompileOptions = ['-feature', '-language:implicitConversions', '-language:higherKinds']
project.configurations.scalaCompiler.files.each { scalaCompileOptions += "-Xplugin:${it.path}".toString() }
compileScala.scalaCompileOptions.additionalParameters = scalaCompileOptions
compileTestScala.scalaCompileOptions.additionalParameters = scalaCompileOptions

compileScala.scalaCompileOptions.forkOptions.with {
    memoryMaximumSize = '1g'
    jvmArgs = ['-Xss16m']
}

compileTestScala.scalaCompileOptions.forkOptions.with {
    memoryMaximumSize = '1g'
    jvmArgs = ['-Xss16m']
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
    distributionType = 'all'
}

pluginBundle {
    website = 'https://github.com/seek-oss/gradle-aws-plugin'
    vcsUrl = 'https://github.com/seek-oss/gradle-aws-plugin'
    def pluginTags = ['SEEK', 'AWS', 'CloudFormation', 'Cloud', 'Config',
                      'Scala', 'Infrastructure', 'Deploy', 'Deployment', 'DevOps']

    plugins {
        awsPlugin {
            id = 'seek.aws'
            displayName = 'AWS Plugin'
            version = baseVersion
            description = 'AWS Plugin for Gradle'
            tags = pluginTags
        }
        cloudFormationPlugin {
            id = 'seek.cloudformation'
            displayName = 'CloudFormation Plugin'
            version = baseVersion
            description = 'CloudFormation Plugin for Gradle'
            tags = pluginTags
        }
    }

    mavenCoordinates {
        version = baseVersion
    }
}

publishPlugins.dependsOn check
